dnl Process this file with autoconf to produce a configure script.
# require autoconf 2.13
AC_PREREQ(2.13)

AC_INIT(sqlplusint/sqlplus.hh)

dnl Much of this was copied from glib's configure.in

dnl we need to AC_DIVERT_PUSH/AC_DIVERT_POP these variable definitions so they
dnl are available for $ac_help expansion (don't we all *love* autoconf?)
AC_DIVERT_PUSH(AC_DIVERSION_NOTICE)dnl
#
# Making releases:
#   SQLPLUS_MICRO_VERSION += 1;
#   SQLPLUS_INTERFACE_AGE += 1;
#   SQLPLUS_BINARY_AGE += 1;
# if any functions have been added, set SQLPLUS_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set SQLPLUS_BINARY_AGE _and_ SQLPLUS_INTERFACE_AGE to 0.
#

SQLPLUS_MAJOR_VERSION=1
SQLPLUS_MINOR_VERSION=5
SQLPLUS_MICRO_VERSION=0
SQLPLUS_INTERFACE_AGE=0
SQLPLUS_BINARY_AGE=0

SQLPLUS_VERSION=$SQLPLUS_MAJOR_VERSION.$SQLPLUS_MINOR_VERSION.$SQLPLUS_MICRO_VERSION
dnl
AC_DIVERT_POP()dnl

VERSION=$SQLPLUS_VERSION

AC_SUBST(SQLPLUS_MAJOR_VERSION)
AC_SUBST(SQLPLUS_MINOR_VERSION)
AC_SUBST(SQLPLUS_MICRO_VERSION)
AC_SUBST(SQLPLUS_INTERFACE_AGE)
AC_SUBST(SQLPLUS_BINARY_AGE)

# libtool versioning
LT_RELEASE=$SQLPLUS_MAJOR_VERSION.$SQLPLUS_MINOR_VERSION
LT_CURRENT=`expr $SQLPLUS_MICRO_VERSION - $SQLPLUS_INTERFACE_AGE`
LT_REVISION=$SQLPLUS_INTERFACE_AGE
LT_AGE=`expr $SQLPLUS_BINARY_AGE - $SQLPLUS_INTERFACE_AGE`
AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

dnl Initialize automake
AM_INIT_AUTOMAKE(sqlplus, $VERSION)

dnl Set definition here
AM_CONFIG_HEADER(config.h)

dnl Initialize libtool
AM_PROG_LIBTOOL

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

dnl This is a little hack to make this work with rpm better (see sqlplus.spec.in)
test -z "$CXXFLAGS" && CXXFLAGS="${CFLAGS}"

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CXXCPP

dnl Copied from lf_texidoc.m4 in share/aclocal
AC_PATH_PROGS(PERL, perl perl5, nope)
if test "$PERL" = nope 
then
   AC_MSG_WARN([missing perl. Need perl to build documentation and some headers which should have been included in the distribution.])
fi


AC_PROG_LN_S
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl These will be set in config.h
AC_DEFINE_UNQUOTED(SQLPLUS_MAJOR_VERSION, $SQLPLUS_MAJOR_VERSION)
AC_DEFINE_UNQUOTED(SQLPLUS_MINOR_VERSION, $SQLPLUS_MINOR_VERSION)
AC_DEFINE_UNQUOTED(SQLPLUS_MICRO_VERSION, $SQLPLUS_MICRO_VERSION)
AC_DEFINE_UNQUOTED(SQLPLUS_INTERFACE_AGE, $SQLPLUS_INTERFACE_AGE)
AC_DEFINE_UNQUOTED(SQLPLUS_BINARY_AGE,    $SQLPLUS_BINARY_AGE)

AC_CHECK_LIB(intl, main)
AC_CHECK_LIB(socket, main)
AC_CHECK_LIB(nsl, main)

dnl #########################################################################
dnl Check for MySQL headers and libraries
dnl #########################################################################
AC_PACKAGE_MYSQL

AM_WITH_DMALLOC()

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_CHECK_FUNCS(strtol)

AC_OUTPUT([
Makefile
sqlplusint/Makefile
examples/Makefile
doc/Makefile
sqlplus-config
sqlplus.spec
])
