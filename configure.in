dnl Process this file with autoconf to produce a configure script.
AC_INIT(sqlplusint/sqlplus.hh)

VERSION=1.7.0
SQLPLUS_MAJOR_VERSION=[`expr $VERSION : '\([0-9][0-9]*\)'`]
AC_SUBST(SQLPLUS_MAJOR_VERSION)
SQLPLUS_MINOR_VERSION=[`expr $VERSION : '[0-9][0-9]*\.\([0-9][0-9]*\)'`]
AC_SUBST(SQLPLUS_MINOR_VERSION)
SQLPLUS_MICRO_VERSION=[`expr $VERSION : '[0-9][0-9]*\.[0-9][0-9]*\.\([0-9][0-9]*\)'`]
AC_SUBST(SQLPLUS_MICRO_VERSION)
AM_INIT_AUTOMAKE(sqlplus, $VERSION)

AM_CONFIG_HEADER(config.h)

AC_PROG_MAKE_SET

AM_PROG_LIBTOOL

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

#
# Location of MySQL installation
#
AC_ARG_WITH(mysql,
    [  --with-mysql=<path> root directory path of MySQL installation],
    [MYSQL_HOME_check=$with_mysql],
    [MYSQL_HOME_check="/usr/local/mysql /usr/local /opt/mysql /usr"])

AC_MSG_CHECKING([for MySQL home directory])
MYSQL_HOME=
for m in $MYSQL_HOME_check; do
        if test -d "$m/include/mysql" && test -d "$m/lib/mysql"
        then
                MYSQL_HOME=$m
                break
        fi
done

if test -z "$MYSQL_HOME"; then
        AC_MSG_ERROR([Didn't find the mysql include and lib dirs in '$MYSQL_HOME_check'])
fi

case "$MYSQL_HOME" in
  /* ) ;;
  * )  AC_MSG_ERROR([The MySQL directory ($MYSQL_HOME) must be a full path.]) ;;
esac

AC_MSG_RESULT([$MYSQL_HOME])
AC_SUBST(MYSQL_HOME)

LDFLAGS="$LDFLAGS -L${MYSQL_HOME}/lib/mysql"
CPPFLAGS="$CPPFLAGS -I${MYSQL_HOME}/include/mysql"

AC_PROG_CC
AC_PROG_CXX

AC_CHECK_LIB(intl, main)
AC_CHECK_LIB(socket, main)
AC_CHECK_LIB(nsl, main)
AC_CHECK_LIB(mysqlclient, mysql_store_result)
AM_WITH_DMALLOC()

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_CHECK_FUNCS(strtol)

AC_OUTPUT(Makefile sqlplusint/Makefile examples/Makefile)
