dnl Process this file with autoconf to produce a configure script.
# require autoconf 2.13
AC_PREREQ(2.13)

AC_INIT(sqlplusint/sqlplus.hh)

dnl Much of this was copied from glib's configure.in

dnl we need to AC_DIVERT_PUSH/AC_DIVERT_POP these variable definitions so they
dnl are available for $ac_help expansion (don't we all *love* autoconf?)
AC_DIVERT_PUSH(AC_DIVERSION_NOTICE)dnl
#
# Making releases:
#   MYSQLPLUS_MICRO_VERSION += 1;
#   MYSQLPLUS_INTERFACE_AGE += 1;
#   MYSQLPLUS_BINARY_AGE += 1;
# if any functions have been added, set MYSQLPLUS_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set MYSQLPLUS_BINARY_AGE _and_ MYSQLPLUS_INTERFACE_AGE to 0.
#

MYSQLPLUS_MAJOR_VERSION=1
MYSQLPLUS_MINOR_VERSION=6
MYSQLPLUS_MICRO_VERSION=0
MYSQLPLUS_INTERFACE_AGE=0
MYSQLPLUS_BINARY_AGE=0

dnl For my rpm.m4 macros
RPM_RELEASE=1

MYSQLPLUS_VERSION=$MYSQLPLUS_MAJOR_VERSION.$MYSQLPLUS_MINOR_VERSION.$MYSQLPLUS_MICRO_VERSION
dnl
AC_DIVERT_POP()dnl

VERSION=$MYSQLPLUS_VERSION
AC_SUBST(RPM_RELEASE)

AC_SUBST(MYSQLPLUS_MAJOR_VERSION)
AC_SUBST(MYSQLPLUS_MINOR_VERSION)
AC_SUBST(MYSQLPLUS_MICRO_VERSION)
AC_SUBST(MYSQLPLUS_INTERFACE_AGE)
AC_SUBST(MYSQLPLUS_BINARY_AGE)
AC_SUBST(MYSQLPLUS_VERSION)

# libtool versioning
LT_RELEASE=$MYSQLPLUS_MAJOR_VERSION.$MYSQLPLUS_MINOR_VERSION
LT_CURRENT=`expr $MYSQLPLUS_MICRO_VERSION - $MYSQLPLUS_INTERFACE_AGE`
LT_REVISION=$MYSQLPLUS_INTERFACE_AGE
LT_AGE=`expr $MYSQLPLUS_BINARY_AGE - $MYSQLPLUS_INTERFACE_AGE`
AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

dnl Set info about the system
AC_CANONICAL_HOST

dnl Initialize automake
dnl FYI, I believe, the [] are necessary for quoting things correctly
dnl for M4's sake.
AM_INIT_AUTOMAKE([mysql++], $VERSION)

dnl Set definition here
AM_CONFIG_HEADER(config.h)

dnl Initialize libtool
AM_PROG_LIBTOOL

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

dnl This is a little hack to make this work with rpm better (see mysql++.spec.in)
test -z "$CXXFLAGS" && CXXFLAGS="${CFLAGS}"

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CXXCPP

dnl Copied from lf_texidoc.m4 in share/aclocal
AC_PATH_PROGS(PERL, perl perl5, nope)
if test "$PERL" = nope 
then
   AC_MSG_WARN([missing perl. Need perl to build documentation and some headers which should have been included in the distribution.])
fi

AC_PROG_LN_S
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AM_RPM_INIT
dnl Enable or disable the rpm making rules in Makefile.am
AM_CONDITIONAL(MAKE_RPMS, test x$make_rpms = xtrue)

dnl These will be set in config.h
AC_DEFINE_UNQUOTED(MYSQLPLUS_MAJOR_VERSION, $MYSQLPLUS_MAJOR_VERSION)
AC_DEFINE_UNQUOTED(MYSQLPLUS_MINOR_VERSION, $MYSQLPLUS_MINOR_VERSION)
AC_DEFINE_UNQUOTED(MYSQLPLUS_MICRO_VERSION, $MYSQLPLUS_MICRO_VERSION)
AC_DEFINE_UNQUOTED(MYSQLPLUS_INTERFACE_AGE, $MYSQLPLUS_INTERFACE_AGE)
AC_DEFINE_UNQUOTED(MYSQLPLUS_BINARY_AGE,    $MYSQLPLUS_BINARY_AGE)

AC_CHECK_LIB(intl, main)
AC_CHECK_LIB(socket, main)
AC_CHECK_LIB(nsl, main)

dnl #########################################################################
dnl Check for MySQL headers and libraries
dnl #########################################################################
dnl I really do not know which version of mysql is required... this is
dnl what I had installed
AM_PATH_MYSQL(3.22.21,,
    AC_MSG_ERROR([
*** MySQL 3.22.21 or better is required. The latest version of MySQL
*** is always available from http://mysql.com/.]))

AM_WITH_DMALLOC()

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_CHECK_FUNCS(strtol)

if test x$CXX != x; then
INSTALLED_CONFIG_HEADER="$PACKAGE"-config.hh
else
INSTALLED_CONFIG_HEADER="$PACKAGE"-config.h
fi

AC_SUBST(INSTALLED_CONFIG_HEADER)

AC_OUTPUT_COMMANDS([

## Generate $INSTALLED_CONFIG_HEADER
echo creating $INSTALLED_CONFIG_HEADER
outfile="$INSTALLED_CONFIG_HEADER"-tmp

header_guard=`echo $INSTALLED_CONFIG_HEADER | tr 'a-z-.' 'A-Z__' | sed 's/+/PLUS/g'`

cat > $outfile <<_______EOF
#ifndef $header_guard
#define $header_guard

#define MYSQLPLUS_MAJOR_VERSION     $MYSQLPLUS_MAJOR_VERSION
#define MYSQLPLUS_MINOR_VERSION     $MYSQLPLUS_MINOR_VERSION
#define MYSQLPLUS_MICRO_VERSION     $MYSQLPLUS_MICRO_VERSION

#define MYSQLPLUS_VERSION          "$MYSQLPLUS_VERSION"

#define MYSQLPLUS_INTERFACE_AGE     $MYSQLPLUS_INTERFACE_AGE
#define MYSQLPLUS_BINARY_AGE        $MYSQLPLUS_BINARY_AGE

#endif /*  $header_guard */
_______EOF

if cmp -s $outfile $INSTALLED_CONFIG_HEADER; then
  echo $INSTALLED_CONFIG_HEADER is unchanged
  rm -f $outfile
else
  mv $outfile $INSTALLED_CONFIG_HEADER
fi
],[
MYSQLPLUS_MAJOR_VERSION=$MYSQLPLUS_MAJOR_VERSION
MYSQLPLUS_MINOR_VERSION=$MYSQLPLUS_MINOR_VERSION
MYSQLPLUS_MICRO_VERSION=$MYSQLPLUS_MICRO_VERSION

MYSQLPLUS_INTERFACE_AGE=$MYSQLPLUS_INTERFACE_AGE
MYSQLPLUS_BINARY_AGE=$MYSQLPLUS_BINARY_AGE

MYSQLPLUS_VERSION=$MYSQLPLUS_VERSION

INSTALLED_CONFIG_HEADER=$INSTALLED_CONFIG_HEADER
])

AC_OUTPUT([
Makefile
sqlplusint/Makefile
examples/Makefile
doc/Makefile
macros/Makefile
mysql++-config
mysql++.spec
],
[
chmod 0755 mysql++-config
test -f mysql-config && chmod 0755 mysql-config
])
